# Namespace:    Hammurabi.Econ
# Summary:      Basic economic relationships related to employment.
# Updated:      2012-06-23
# Author:       Michael Poulshock

# NATURE OF EMPLOYMENT RELATIONSHIP

# Is employed
Tbool IsEmployed(Person p) =
	Stub()
	# Exists(AllCorps, IsEmployedBy(p,_))
	
# Indicates whether a person is employed by a particular employer (corporation).  
# It is assumed that "employee" does not include independent contractors.
Tbool IsEmployedBy(Person p, Thing corp) =
	TstrIn EmploymentRelationship(p, corp) == "Employee" 

# Person is an independent contractor at an employer
Tbool IsIndependentContractor(Person p, Thing corp) =
	TstrIn EmploymentRelationship(p, corp) == "Independent contractor"
        
# Returns the date a person started working at an employer
TdateIn DateStartedWorkAt(Person p, Thing corp)

# Person is a full-time employee
TboolIn IsFullTimeEmployee(Person p, Thing corp) =
	set:
	if !IsEmployedBy(p, corp) -> false
	if hours >= 35 -> true
	if hours <= 30 -> false
	else Stub()
	
	Tnum hours = 
		HoursWorkedPerWeek(p, corp)

# Number of hours per week that a person works at a given employer
TnumIn HoursWorkedPerWeek(Person p, Thing corp)
	
# Length of an employee's initial probationary period (in months)
TnumIn InitialProbationLength(Person p, Thing corp)


# UNEMPLOYMENT COMPENSATION
# Source: http://www.twc.state.tx.us/news/efte/ui_law_qualification_issues.html#dq-layoffs
	
# Disqualified from unemployment insurance
Tbool DQFromUnemploymentInsurance(Person p, Thing corp) =
	TerminatedFrom(p, corp) |
	ResignedFrom(p, corp) |
	TboolIn RefusesSuitableWorkWithoutGoodCause(p) |
	IsOnStrikeFrom(p, corp) |
	IsReceivingSeveranceFrom(p, corp).IsEverTrue() |
	IsReceivingWorkersComp(p)
	
	
# SEPARATION FROM AN EMPLOYER
# Source: http://www.twc.state.tx.us/news/efte/types_of_work_separations.html

# Medical reasons...
# Strike

# Voluntary separation
Tbool VoluntarilySeparatedFrom(Person p, Thing corp) =
	ResignedFrom(p, corp) |
	VoluntarilyRetiredFrom(p, corp) |
	ConstructivelyDischargedFrom(p, corp) |
	PresumptivelyResignedFrom(p, corp)
	
# Involuntary separation
Tbool InvoluntarilySeparatedFrom(Person p, Thing corp) =
	TerminatedFrom(p, corp) |
	ForciblyRetiredFrom(p, corp) |
	ResignedInLieuOfDischargeFrom(p, corp) |
	LaidOffFrom(p, corp) |
	TempJobEndedAt(p, corp) |
	MutuallyAgreedSeparationFrom(p, corp) |
	PresumptivelyTerminatedFrom(p, corp)
	
# Quit/resigned from an employer
Tbool ResignedFrom(Person p, Thing corp) =
	NatureOfSeparation(p, corp) == "Resignation"

# Resignation in lieu of discharge
Tbool ResignedInLieuOfDischargeFrom(Person p, Thing corp) =
	NatureOfSeparation(p, corp) == "Resignation in lieu of discharge"

# Discharge or termination for misconduct or "cause" 
Tbool TerminatedFrom(Person p, Thing corp) =
	NatureOfSeparation(p, corp) == "Termination for misconduct or cause"
	
# Laid off
Tbool LaidOffFrom(Person p, Thing corp) =
	NatureOfSeparation(p, corp) == "Layoff, reduction in force, or downsizing"
	
# Retired from an employer
Tbool RetiredFrom(Person p, Thing corp) =
	VoluntarilyRetiredFrom(p, corp) |
	ForciblyRetiredFrom(p, corp)

# Temporary job comes to an end
Tbool TempJobEndedAt(Person p, Thing corp) =
	NatureOfSeparation(p, corp) == "Temporary job ended"
	
# Voluntarily retired from an employer
Tbool VoluntarilyRetiredFrom(Person p, Thing corp) =
	NatureOfSeparation(p, corp) == "Voluntary retirement"

# Forced to retire from an employer
Tbool ForciblyRetiredFrom(Person p, Thing corp) =
	NatureOfSeparation(p, corp) == "Forced retirement"

# Working conditions so intolerable that a reasonable employee would feel forced to resign
Tbool ConstructivelyDischargedFrom(Person p, Thing corp) =
	NatureOfSeparation(p, corp) == "Constructive discharge"

# Separated from an employer based on mutual agreement
Tbool MutuallyAgreedSeparationFrom(Person p, Thing corp) =
	NatureOfSeparation(p, corp) == "Mutual agreement"

# Presumptively resigned from an employer
Tbool PresumptivelyTerminatedFrom(Person p, Thing corp) =
	NatureOfSeparation(p, corp) == "Failed to return following unpaid suspension" &
	LengthOfSuspensionFrom(p, corp) >= 4		# May vary by jurisdiction!
	
# Presumptively resigned from an employer
Tbool PresumptivelyResignedFrom(Person p, Thing corp) =
	NatureOfSeparation(p, corp) == "Failed to return following unpaid suspension" &
	LengthOfSuspensionFrom(p, corp) < 3		# May vary by jurisdiction!
	
# Length of unpaid suspension (in days)
TnumIn LengthOfSuspensionFrom(Person p, Thing corp) 
	
# Reason person left an employer
TstrIn NatureOfSeparation(Person p, Thing corp)

	
# OTHER	

# Received severance pay or wages in lieu of notice
TboolIn IsReceivingSeveranceFrom(Person p, Thing corp)

# Received severance pay or wages in lieu of notice
TboolIn IsReceivingWorkersComp(Person p) 
	
# On stike from an employer
TboolIn IsOnStrikeFrom(Person p, Thing corp) 
	
# On strike - from any employer
TboolIn IsOnStrike(Person p) 

		

# SELF-SUPPORT

# Manages own financial affairs
TboolIn ManagesOwnAffairs(Person p) =
	PercentSelfSupport(p) > 50 |		# Assumed
	TboolIn ManagesOwnAffairs(p)
	
# Percentage which a person supports themselves financially.
# In percentage points (e.g. 50, not 0.50)
TnumIn PercentSelfSupport(Person p)

# Receives public assistance
TboolIn ReceivesPublicAssistance(Person p)

# Physically unfit for employment
TboolIn PhysicallyUnfitForEmployment(Person p) 

# Mentally unfit for employment
TboolIn MentallyUnfitForEmployment(Person p) 


# RANDOM OCCUPATIONS WITH LEGAL SIGNFICANCE

# U.S. government employee
TboolIn IsUSGovtEmployee(Person p)

# Migrant farmworker
TboolIn IsMigrantOrSeasonalFarmworker(Person p)

# Airline flight crew
TboolIn IsAirlineFlightCrew(Person p, Thing corp) 

# Is employee of Panama Railroad Company (or its successor in title)
TboolIn IsPanamaRailroadEmployee(Person p)


# UNIT TESTS

Test: TboolIn1
- Person p
- Thing c
- IsFullTimeEmployee(p,c) = true
- Econ.IsFullTimeEmployee(p,c).Out =?= true		

Test: Employ
- Person p
- Thing c
- EmploymentRelationship(p,c) = "None"
- Econ.IsEmployedBy(p,c).Out =?= false	

