# Namespace:    Hammurabi.Econ
# Summary:      Basic economic relationships related to households
# Updated:      2012-06-23
# Author:       Michael Poulshock


# HOUSEHOLD-RELATED FACTS	

# Household size
Tnum HouseholdSize(Person p) =
	HouseholdOf(p).Members.Count

# A person's household (set)
Tset HouseholdMembersOf(Person p) =
	HouseholdOf(p).Members

# A person's household (entity)
Entity HouseholdOf(Person p) =
	new Entity(members)
	
	Tset members =
		set:
		if hh.IsKnown -> hh
		else AllPeople.Filter(ShareHousehold(_,p))	# sym
	
	Tset hh =
		TsetIn HouseholdOf(p)
	
# Two people are members of the same household.
Tbool ShareHousehold(Person p1, Person p2) =
	LivesWith(p1, p2) 	# simplifying assumption
				 
# Two people share a principal abode.
TboolInSym SharePrincipalAbode(Person p1, Person p2) =
	LivesWith(p1, p2) 	# simplifying assumption

# Live together	
TboolInSym LivesWith(Person p1, Person p2)

# Returns whether one person provides financial support for another.
TboolIn ProvidesSupportFor(Person p1, Person p2)

# Purchase and prepare meals together for home consumption
TboolIn PurchaseAndPrepareMealsTogether(Person p1, Person p2) 


# UNIT TESTS

Test: TboolInSym1
- Person a
- Person b
- SharePrincipalAbode(a,b) = true
- Econ.SharePrincipalAbode(a,b).Out =?= true

Test: TboolInSym2
- Person a
- Person b
- SharePrincipalAbode(a,b) = true
- Econ.SharePrincipalAbode(b,a).Out =?= true

Test: ShareAbode
- Person a
- Person b
- LivesWith(a,b) = true
- Econ.SharePrincipalAbode(b,a).Out =?= true

Test: LivesWith1
- Person a
- Person b
- LivesWith(a,b) = true
- Econ.LivesWith(a,b).Out =?= true

Test: LivesWith2
- Person a
- Person b
- LivesWith(a,b) = true
- Econ.LivesWith(b,a).Out =?= true

