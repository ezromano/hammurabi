# Namespace:    Hammurabi.Fam
# Summary:      Basic family relationships
# Updated:      2012-08-21
# Author:       Michael Poulshock


# FAMILY MEMBERS

# >>Who are {1}'s family members?
TsetIn FamilyOf(Thing person)


# BASIC FAMILY RELATIONSHIPS
# Marital and parent-child relationships are elsewhere
	
# Siblings
Tbool AreSiblings(Thing p1, Thing p2) =
	Facts.Sym(p1, "FamilyRelationship", p2, "Sibling")    # or, share a parent

# Half-siblings
Tbool AreHalfSiblings(Thing p1, Thing p2) =
	Facts.Sym(p1, "FamilyRelationship", p2, "Half sibling")  # or, share one parent

# Step-siblings
Tbool AreStepsiblings(Thing p1, Thing p2) =
	Facts.Sym(p1, "FamilyRelationship", p2, "Stepsibling")
	
# Grandparent
Tbool IsGrandparentOf(Thing p1, Thing p2) =
	Facts.Sym(p1, "FamilyRelationship", p2, "Grandparent", "Grandchild")
	
# Great-grandparent
Tbool IsGreatGrandparentOf(Thing p1, Thing p2) =
	Facts.Sym(p1, "FamilyRelationship", p2, "Great-grandparent", "Great-grandchild")
  
# Great-great-grandparent
Tbool IsGreatGreatGrandparentOf(Thing p1, Thing p2) =
	Facts.Sym(p1, "FamilyRelationship", p2, "Great-great-grandparent", "Great-great-grandchild")
        
# Aunt or uncle
Tbool IsAuntOrUncleOf(Thing p1, Thing p2) =
	Facts.Sym(p1, "FamilyRelationship", p2, "Aunt or uncle", "Niece or nephew")
        
# Great aunt or uncle
Tbool IsGreatAuntOrUncleOf(Thing p1, Thing p2) =
	Facts.Sym(p1, "FamilyRelationship", p2, "Great aunt or uncle", "Grand niece or nephew")
        
# Great-great aunt or uncle
Tbool IsGreatGreatAuntOrUncleOf(Thing p1, Thing p2) =
	Facts.Sym(p1, "FamilyRelationship", p2, "Great-great aunt or uncle", "Great-grand niece or nephew")
        
# Cousins
Tbool IsCousinOf(Thing p1, Thing p2) =
	IsFirstCousinOf(p1, p2) |
	IsNonFirstCousinOf(p1, p2)
        
# First cousins
Tbool IsFirstCousinOf(Thing p1, Thing p2) =
	Facts.Sym(p1, "FamilyRelationship", p2, "First cousin")
        
# Cousins, but not first cousins
Tbool IsNonFirstCousinOf(Thing p1, Thing p2) =
	Facts.Sym(p1, "FamilyRelationship", p2, "Other cousin")
        
# >>Is {1} an ancestor of {2}?
Tbool IsAncestorOf(Thing p1, Thing p2) =
	TboolIn IsAncestorOf(p1, p2)
	# or inferred

# >>What is/was {1}'s family relationship to {2} (if any)?
Tstr Relationship(Thing p1, Thing p2) =
	TstrIn FamilyRelationship(p1,p2)
	

# CUSTODY, GUARDIANSHIP, ETC.	

# >>Does/did {1} have legal custody over {2}?
TboolIn HasCustodyOf(Thing p1, Thing p2)

# Ward (minor)
Tbool IsWardOf(Thing child, Thing adult) =
	IsMinor(child) &
	IsLegalGuardianOf(adult, child)

# >>Is/was {1} a (general) legal guardian of {2}?
TboolIn IsLegalGuardianOf(Thing p1, Thing p2) =
	CanBeAGuardianOf(p1, p2) &
	TboolIn IsLegalGuardianOf(p1, p2)
        
# >>Does/did {1} act in loco parentis with respect to {2}?
TboolIn ActsInLocoParentisOf(Thing p1, Thing p2) =
	!IsParentOf(p2,p1) &
	TboolIn ActsInLocoParentisOf(p1, p2)

# >>Does/did {1} have day-to-day responsibility for {2}?
Tbool HasDayToDayResponsibilityFor(Thing p1, Thing p2) =
	CanBeAGuardianOf(p1, p2) &
	TboolIn HasDayToDayResponsibilityFor(p1, p2)

# Person can be the guardian of another
Tbool CanBeAGuardianOf(Thing guard, Thing ward) =
	Age(guard) >= 16 &						
	...
		IsDisabled(ward) |					# Disablity (need to clarify)
		...
			Age(ward) < 18 &				# Infancy...
			Age(guard) > Age(ward) &
			!IsParentOf(ward, guard) &		
			!InCivilUnion(guard, ward) &
			!IsDomesticPartnerOf(guard, ward) &
			!ExSpouses(guard, ward)

# Custodial parent
Tbool IsCustodialParentOf(Thing p1, Thing p2) =
	IsParentOf(p1,p2) &
	HasCustodyOf(p1,p2)


# OTHER

# >>Is {1} the next of kin (nearest blood relative) of {2}?
TboolIn IsNextOfKinOf(Thing p1, Thing p2) 
	# or inferred?
	
# Lives with any parent, custodian, or guardian (PCG)
Tbool LivesWithPCG(Thing p) =
	members.Exists(IsLivingWithPCG(p,_))

	Tset members =
		Econ.HouseholdMembersOf(p) - p
	
# Person (p1) lives with parent, custodian, or guardian (PCG)
Tbool IsLivingWithPCG(Thing p1, Thing p2) =
	Econ.LivesWith(p1, p2) &
	...
		IsParentOf(p2, p1) |
		HasCustodyOf(p2, p1) |
		IsLegalGuardianOf(p2, p1)

# Cohabitation
Tbool AreCohabitating(Thing p1, Thing p2) =
	Econ.LivesWith(p1, p2) &
	AreInIntimateRelationship(p1, p2)

# >>Do/did {1} and {2} have a marriage contract or agreement?
TboolInSym HaveMarriageAgreement(Thing p1, Thing p2)	

# >>Do/did {1} and {2} hold each other out to the community as spouses?
TboolInSym HeldOutAsSpouses(Thing p1, Thing p2)
	
# >>Are/were {1} and {2} in an intimate relationship?
TboolInSym AreInIntimateRelationship(Thing p1, Thing p2)

# >>Does/did {1} care for an incapacitated person?
TboolIn CaresForIncapacitatedPerson(Thing p) 


# UNIT TESTS

Test: 49171719
- Things t1, t2
- Things sam, john
- HouseholdMembersOf(t1) = [[sam, john]]
- LivesWith(sam,t1) = true
- IsParentOf(sam,t1) = false
- HasCustodyOf(sam,t1) = false
- IsLegalGuardianOf(sam,t1) = true
- LivesWith(john,t1) = false
- Hammurabi.Fam.LivesWithPCG(t1).TestOutput =?= "Time.DawnOf True "

Test: 135354455
- Things t1, t2
- FamilyRelationship(t2,t1) = "First cousin"
- Hammurabi.Fam.IsCousinOf(t1, t2).TestOutput =?= "Time.DawnOf True "

Test: Sanity1a	
- Thing a
- Thing b
- IsParentOf(a,b) = true
- Fam.IsParentOf(a,b).Out =?= true

Test: Sanity1b	
- Thing a
- Thing b
- IsParentOf(a,b) = true
- Fam.IsChildOf(b,a).Out =?= true

Test: Sanity2
- Thing p
- IsMarried(p) = true
- Fam.IsMarried(p).Out =?= true

Test: ShortCircuit1
- Thing p1
- Thing p2
- ActsInLocoParentisOf(p1,p2) = true
- Fam.ActsInLocoParentisOf(p1,p2).Out =?= true

Test: ShortCircuit2
- Thing p1
- Thing p2
- ActsInLocoParentisOf(p1,p2) = false
- Fam.ActsInLocoParentisOf(p1,p2).Out =?= false

Test: ShortCircuit3
- Thing p1
- Thing p2
- IsParentOf(p2,p1) = true
- Fam.ActsInLocoParentisOf(p1,p2).Out =?= false



