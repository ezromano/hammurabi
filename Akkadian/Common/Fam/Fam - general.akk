# Namespace:    Hammurabi.Fam
# Summary:      Basic family relationships
# Updated:      2012-08-01
# Author:       Michael Poulshock


# FAMILY MEMBERS

# Family members of a given person
TsetIn FamilyOf(Thing person) = 
	Stub()


# BASIC FAMILY RELATIONSHIPS
# Marital and parent-child relationships are elsewhere
	
# Siblings
Tbool AreSiblings(Thing p1, Thing p2) =
	Facts.Sym(p1, "FamilyRelationship", p2, "Sibling")    # or, share a parent

# Half-siblings
Tbool AreHalfSiblings(Thing p1, Thing p2) =
	Facts.Sym(p1, "FamilyRelationship", p2, "Half sibling")  # or, share one parent

# Step-siblings
Tbool AreStepsiblings(Thing p1, Thing p2) =
	Facts.Sym(p1, "FamilyRelationship", p2, "Stepsibling")
	
# Grandparent
Tbool IsGrandparentOf(Thing p1, Thing p2) =
	Facts.Sym(p1, "FamilyRelationship", p2, "Grandparent", "Grandchild")
	
# Great-grandparent
Tbool IsGreatGrandparentOf(Thing p1, Thing p2) =
	Facts.Sym(p1, "FamilyRelationship", p2, "Great-grandparent", "Great-grandchild")
  
# Great-great-grandparent
Tbool IsGreatGreatGrandparentOf(Thing p1, Thing p2) =
	Facts.Sym(p1, "FamilyRelationship", p2, "Great-great-grandparent", "Great-great-grandchild")
        
# Aunt or uncle
Tbool IsAuntOrUncleOf(Thing p1, Thing p2) =
	Facts.Sym(p1, "FamilyRelationship", p2, "Aunt or uncle", "Niece or nephew")
        
# Great aunt or uncle
Tbool IsGreatAuntOrUncleOf(Thing p1, Thing p2) =
	Facts.Sym(p1, "FamilyRelationship", p2, "Great aunt or uncle", "Grand niece or nephew")
        
# Great-great aunt or uncle
Tbool IsGreatGreatAuntOrUncleOf(Thing p1, Thing p2) =
	Facts.Sym(p1, "FamilyRelationship", p2, "Great-great aunt or uncle", "Great-grand niece or nephew")
        
# Cousins
Tbool IsCousinOf(Thing p1, Thing p2) =
	IsFirstCousinOf(p1, p2) |
	IsNonFirstCousinOf(p1, p2)
        
# First cousins
Tbool IsFirstCousinOf(Thing p1, Thing p2) =
	Facts.Sym(p1, "FamilyRelationship", p2, "First cousin")
        
# Cousins, but not first cousins
Tbool IsNonFirstCousinOf(Thing p1, Thing p2) =
	Facts.Sym(p1, "FamilyRelationship", p2, "Other cousin")
        
# Ancestor
Tbool IsAncestorOf(Thing p1, Thing p2) =
	TboolIn IsAncestorOf(p1, p2)
	# or inferred

# String indicating the family relationship between two people.
Tstr Relationship(Thing p1, Thing p2) =
	TstrIn FamilyRelationship(p1,p2)
	

# CUSTODY, GUARDIANSHIP, ETC.	

# Legal custody
TboolIn HasCustodyOf(Thing p1, Thing p2)

# Ward (minor)
Tbool IsWardOf(Thing child, Thing adult) =
	IsMinor(child) &
	IsLegalGuardianOf(adult, child)

# Legal guardian (general)
TboolIn IsLegalGuardianOf(Thing p1, Thing p2) =
	CanBeAGuardianOf(p1, p2) &
	# !NotAGuardianOf(p1, p2) &
	TboolIn IsLegalGuardianOf(p1, p2)
        
# In loco parentis
TboolIn ActsInLocoParentisOf(Thing p1, Thing p2) =
	!IsParentOf(p2,p1) &
	TboolIn ActsInLocoParentisOf(p1, p2)

# Day-to-day responsibility
Tbool HasDayToDayResponsibilityFor(Thing p1, Thing p2) =
	CanBeAGuardianOf(p1, p2) &
	TboolIn HasDayToDayResponsibilityFor(p1, p2)

# Person can be the guardian of another
Tbool CanBeAGuardianOf(Thing guard, Thing ward) =
	Age(guard) >= 16 &						
	...
		IsDisabled(ward) |					# Disablity (need to clarify)
		...
			Age(ward) < 18 &				# Infancy...
			Age(guard) > Age(ward) &
			!IsParentOf(ward, guard) &		
			!InCivilUnion(guard, ward) &
			!IsDomesticPartnerOf(guard, ward) &
			!ExSpouses(guard, ward)

# Custodial parent
Tbool IsCustodialParentOf(Thing p1, Thing p2) =
	IsParentOf(p1,p2) &
	HasCustodyOf(p1,p2)


# OTHER

# Next of kin (nearest blood relative)
TboolIn IsNextOfKinOf(Thing p1, Thing p2) 
	# or inferred?
	
# Lives with any parent, custodian, or guardian (PCG)
Tbool LivesWithPCG(Thing p) =
	Stub()
	# members.Exists(IsLivingWithPCG(p,_))

	Tset members =
		Econ.HouseholdMembersOf(p) - p
	
# Person (p1) lives with parent, custodian, or guardian (PCG)
Tbool IsLivingWithPCG(Thing p1, Thing p2) =
	Econ.LivesWith(p1, p2) &
	...
		IsParentOf(p2, p1) |
		HasCustodyOf(p2, p1) |
		IsLegalGuardianOf(p2, p1)

# Cohabitation
Tbool AreCohabitating(Thing p1, Thing p2) =
	Econ.LivesWith(p1, p2) &
	AreInIntimateRelationship(p1, p2)

# Have a marriage contract or agreement
TboolInSym HaveMarriageAgreement(Thing p1, Thing p2)	

# Held each other out to the community as spouses
TboolInSym HeldOutAsSpouses(Thing p1, Thing p2)
	
# Intimate together
TboolInSym AreInIntimateRelationship(Thing p1, Thing p2)

# Cares for an incapacitated person
TboolIn CaresForIncapacitatedPerson(Thing p) 


# UNIT TESTS

Test: Sanity1a	
- Thing a
- Thing b
- IsParentOf(a,b) = true
- Fam.IsParentOf(a,b).Out =?= true

Test: Sanity1b	
- Thing a
- Thing b
- IsParentOf(a,b) = true
- Fam.IsChildOf(b,a).Out =?= true

Test: Sanity2
- Thing p
- IsMarried(p) = true
- Fam.IsMarried(p).Out =?= true

Test: ShortCircuit1
- Thing p1
- Thing p2
- ActsInLocoParentisOf(p1,p2) = true
- Fam.ActsInLocoParentisOf(p1,p2).Out =?= true

Test: ShortCircuit2
- Thing p1
- Thing p2
- ActsInLocoParentisOf(p1,p2) = false
- Fam.ActsInLocoParentisOf(p1,p2).Out =?= false

Test: ShortCircuit3
- Thing p1
- Thing p2
- IsParentOf(p2,p1) = true
- Fam.ActsInLocoParentisOf(p1,p2).Out =?= false

