# Namespace:    Hammurabi.H
# Summary:      Basic facts about individual people.
# Updated:      2012-07-22
# Author:       Michael Poulshock


# BIRTH, LIFE, AND DEATH

# Date of birth
TdateIn DoB(Thing p) =
	# >> What is {1}'s date of birth?
	TdateIn DateOfBirth(p)

# Date of death
# >> What is {1}'s date of death?
TdateIn DateOfDeath(Thing p)

# Deceased
# >> Is {1} deceased?
TboolIn IsDeceased(Thing p)

# Age (in years)
Tnum Age(Thing p) =
	Time.IntervalsSince(DoB(p),
	                    DoB(p).AddYears(110),
						Time.IntervalType.Year)
						
# Alive
Tbool IsAlive(Thing p) =
	...
		!IsDeceased(p) &
		TheTime.IsBetween(DoB(p), DoB(p).AddYears(120))
	|
	...
		IsDeceased(p) &
		TheTime.IsBetween(DoB(p), DateOfDeath(p))
		
# Age in months (temporal)
Tnum AgeInMonths(Thing p) =
	Time.IntervalsSince(DoB(p), DoB(p).AddYears(110), Time.IntervalType.Month)

		
# GENDER

# Female
Tbool IsFemale(Thing p) =
	Gender(p) == "Female"

# Male
Tbool IsMale(Thing p) =
	Gender(p) == "Male"

# Transgendered
Tbool IsTransgendered(Thing p) =
	Gender(p) == "Transgendered"		

# Gender
TstrIn Gender(Thing p)


# ETHNICITY

# Member of a Native American tribe
# Includes Alaska Natives and Native Hawaiians
# Note: Most legal sources use "Indian" or "American Indian"
# >> Is {1} a member of a Native American, Native Hawaiian, or Alaska Native tribe?
TboolIn IsNativeAmerican(Thing p)


# MISCELLANEOUS

# Has a Social Security Number
TboolIn HasSSN(Thing p) 
	
# Has applied for (but not yet received) an SSN
TboolIn AppliedForSSN(Thing p) 

# Blind (see 42 USC 416(i)(1)(B))
TboolIn IsBlind(Thing p)

# Disabled 
# Has a physical or mental impairment that substantially 
# limits one or more major life activities (see 42 USC 12102)
TboolIn IsDisabled(Thing p)

# Incapable of self-care
TboolIn IsIncapableOfSelfCare(Thing p) 

# In prison
TboolIn IsInPrison(Thing p)

# In juvenile rehab facility
TboolIn InJuvenileFacility(Thing p)

# In a drug or alcohol treatment program
TboolIn InDrugOrAlcoholTreatmentProg(Thing p) 
	
	
# UNIT TESTS

Test: TotalSanity
- Thing p
- DateOfBirth(p) = 1940-01-02
- DoB(p).Out =?= 1940-01-02
							
Test: Alive1
- Thing p
- IsAlive(p).Out =?= "Unstated"

Test: Alive2a
- Thing p
- DateOfBirth(p) = 2000-01-01
- IsDeceased(p) = false
- IsAlive(p).AsOf(2010-01-01).Out =?= true

Test: Alive2b
- Thing p
- DateOfBirth(p) = 2000-01-01
- IsDeceased(p) = false
- IsAlive(p).AsOf(2150-01-01).Out =?= false

Test: Alive2c
- Thing p
- DateOfBirth(p) = 1999-01-01
- IsDeceased(p) = false
- IsAlive(p).AsOf(2010-01-01).Out =?= true

Test: Alive3a
- Thing p
- DateOfBirth(p) = 2000-01-01
- DateOfDeath(p) = 2090-01-01
- IsDeceased(p) = true
- IsAlive(p).AsOf(2100-01-01).Out =?= false

Test: Alive3b
- Thing p
- DateOfBirth(p) = 2000-01-01
- DateOfDeath(p) = 2090-01-01
- IsDeceased(p) = true
- IsAlive(p).AsOf(2050-01-01).Out =?= true

Test: Alive3c
- Thing p
- DateOfBirth(p) = 2000-01-01
- DateOfDeath(p) = 2090-01-01
- IsDeceased(p) = true
- IsAlive(p).AsOf(1999-01-01).Out =?= false

	
