# Namespace:    Hammurabi.H
# Summary:      Facts about individual people relating to residence, location, and U.S. citizenship.
# Updated:      2012-09-22
# Author:       Michael Poulshock


# PLACE OF RESIDENCE

# U.S. resident
Tbool IsUSResident(Thing p) =
    CountryOfResidence(p) == "United States" |
    ResidesInUSState(p)
    
ResidesInUSState(1) assumes CountryOfResidence(1) == "United States"

# Resides in U.S. state
Tbool ResidesInUSState(Thing p) =
    Geo.IsUSState(StateOfResidence(p))
    
# Resident of a given state or country
Tbool IsResidentOf(Thing p, string jur) =
    CountryOfResidence(p) == jur |
    StateOfResidence(p) == jur

# >>In which country or territory does/did {1} reside?
Tstr CountryOfResidence(Thing p)

# >>In what state does/did {1} reside?
Tstr StateOfResidence(Thing p) 


# U.S. CITIZENSHIP

# U.S. citizen
Tbool IsUSCitizen(Thing p) =
    Imm.Status(p) == "U.S. citizen"     

# >>Is {1} subject to U.S. jurisdiction?
Tbool IsSubjectToUSJurisdiction(Thing p)


# BIRTH LOCATION

# Born in U.S.
Tbool BornInTheUSA(Thing p) =
    BirthCountry(p) == "United States"

# >>In what country was {1} born?
Tstr BirthCountry(Thing p)

# >>In what U.S. state or territory was {1} born?  (This includes places like Guam, Puerto Rico, and the Panama Canal Zone.)
Tstr BirthState(Thing p)


# LOCATION

# Physically present in U.S.
Tbool PresentInUS(Thing p) =
    CountryLocation(p) == "United States"
    
# >>In what country or territory is/was {1} physically located?
Tstr CountryLocation(Thing p)


# UNIT TESTS

Test: 909943367
- Thing jane
- StateOfResidence(jane) = "Louisiana"
- Hammurabi.H.IsUSResident(jane).TestOutput =?= "Time.DawnOf True "

Test: 280315571
- Thing jane
- CountryOfResidence(jane) = "Japan"
- Hammurabi.H.IsUSResident(jane).TestOutput =?= "Time.DawnOf False "

Test: 909943867
- Thing jane
- CountryOfResidence(jane) = "United States"
- Hammurabi.H.IsUSResident(jane).TestOutput =?= "Time.DawnOf True "



