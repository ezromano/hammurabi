# Namespace:    IRS.Pub946
# Summary:      IRS Publication 946 - Depreciating Property
# Updated:      2013-01-06
# Author:       Michael Poulshock


# WHAT PROPERTY CAN BE DEPRECIATED?

# >> Can {1} depreciate {2}?
TboolIn CanDepreciateProperty(Thing person, Thing property) =
    MeetsOwnershipTest(person, property) &
    TboolIn UsesPropertyInBusiness(person, property) &
    TboolIn HasDeterminableUsefulLife(property) &
    TboolIn ExpectedLifeOfMoreThanOneYear(property)
    
    
Tbool MeetsOwnershipTest(Thing person, Thing property) =
    Prop.Owns(person, property) |
    Stub() # lease + incidents of ownership


# WHICH DEPRECIATION SYSTEM (GDS OR ADS) APPLIES?

# Required to use the ADS depreciation system
Tbool MustUseADS(Thing person, Thing property) =
    ...
        IsListedProperty(person, property) &
        TnumIn PercentageQualifiedBusinessUse(person, property) <= 50
    |
    ...
        Prop.IsTangibleProperty(property) &
        TnumIn PercentageUseOutsideUS(person, property) > 50
    |
    TboolIn IsTaxExemptUseProperty(property) |
    TboolIn IsTaxExemptBondFinancedProperty(property) |
    ...
        TnumIn PercentageUseInFarming(person, property) > 50 &
        TboolIn PlacedInServiceWhenElectedNotToApplyUniformCapitalizationRules(person, property)
    |
    ...
        Prop.IsImported(property) &
        TboolIn ImportedFromCountryWithEOTradeRestrictions(property)
        

# >>Does/did {1} elect to use the ADS depreciation system?  (See IRS Pub. 946, p. 36 for conditions.)
TboolIn ElectsToUseADS(Thing person, Thing property)        


# WHICH GDS PROPERTY CLASS APPLIES?

# Applicable GDS property class
Tstr GDSPropertyClass(Thing person, Thing prop) =
    set:
    if TboolIn Is3YearProperty(person, prop) -> "3-year property"
    if TboolIn Is5YearProperty(person, prop) -> "5-year property"
    if TboolIn Is7YearProperty(person, prop) -> "7-year property"
    if TboolIn Is10YearProperty(person, prop) -> "10-year property"
    if TboolIn Is15YearProperty(person, prop) -> "15-year property"
    if TboolIn Is20YearProperty(person, prop) -> "20-year property"
    if TboolIn Is25YearProperty(person, prop) -> "25-year property"
    if TboolIn IsResidentialRentalProperty(person, prop) -> "Residential rental property"
    if TboolIn IsNonresidentialRealProperty(person, prop) -> "Nonresidential real property"
    else Stub()
    
    
# WHICH DEPRECIATION METHOD APPLIES?

# >>What method is {1} using to depreciate {2}? (Options: DB or SL)
Tstr DepreciationMethod(Thing person, Thing property) =
    Stub()
    
    
# COMPUTING THE DEPRECIATION DEDUCTION W/O USING THE TABLES

# Applicable depreciate rate, given the date the property was acquired
# Returns a timeline expressing the amount of the depreciation deduction over time
Tnum DepreciationDeduction(Thing prop) =
    TemporalMap(n => Deduction(prop,n), serviceDate, 10, Time.IntervalType.Year)
    
    Tdate serviceDate = DatePlacedInService(prop)
   
# The following two functions are mutually recursive:    
    
# Basis, adjusted annually for depreciation deductions taken
Tnum AdjustedBasis(Thing prop, Tnum year) =
    set:
    if year == 1 -> UnadjustedBasis(prop)
    else AdjustedBasis(prop, year-1) - Deduction(prop, year-1)
 
# Computed depreciation deduction amount
Tnum Deduction(Thing prop, Tnum year) = 
    set:
    if DepreciationMethod(prop) == "DB" & year < MethodSwitchYear(recPeriod) -> (basis * DecliningBalanceRate(recPeriod) * fractionalYear).RoundToNearest(1)
    if year <= recPeriod + 1 -> (basis * StraightLineRate(yearsLeftInRecoveryPeriod) * fractionalYear).RoundToNearest(1)
    else 0
    
    Tnum basis = AdjustedBasis(prop, year)
    
    Tnum yearsLeftInRecoveryPeriod = 
        set:
        if year == 1 -> recPeriod
        else recPeriod - year + 2 - firstYearPercentage
    
    Tnum fractionalYear =
        set: 
        if year == 1 -> firstYearPercentage
        else 1
        
    Tnum firstYearPercentage = 
        FirstYearPercentage(ApplicableFirstYearConvention(prop), DatePlacedInService(prop))

    Tnum recPeriod = RecoveryPeriod(prop)
    
# Depreciation rate using the declining balance method
Tnum DecliningBalanceRate(Tnum propertyClass) =
    match propertyClass
    3   -> 0.66667
    5   -> 0.400
    7   -> 0.28571
    10  -> 0.200
    15  -> 0.100
    20  -> 0.075
    else Stub()

# Year in which you switch from declining balance to straight line method
Tnum MethodSwitchYear(Tnum propertyClass) =
    match propertyClass
    3   -> 3
    5   -> 4
    7   -> 5
    10  -> 7
    15  -> 7
    20  -> 9
    else Stub()
    
# Depreciation rate using the straight line method
Tnum StraightLineRate(Tnum yearsRemaining) =
    set:
    if yearsRemaining <= 0 -> 0
    if yearsRemaining < 1 -> 1.00
    else (1 / yearsRemaining).RoundToNearest(0.00001)

# Percentage of first-year credited towards deduction, based 
# on the applicable convention and the date the property was 
# placed in service.
Tnum FirstYearPercentage(Tstr convention, Tdate datePlacedInService) =
    set:
    if convention == "Half-year" -> 0.50
    if convention == "Mid-quarter" & serviceQuarter == 1 -> 0.875
    if convention == "Mid-quarter" & serviceQuarter == 2 -> 0.625
    if convention == "Mid-quarter" & serviceQuarter == 3 -> 0.375
    if convention == "Mid-quarter" & serviceQuarter == 4 -> 0.125
    if convention == "Mid-month" -> ((12 - datePlacedInService.Month + 0.50) / 12).RoundToNearest(0.001)
    else Stub()
    
    Tnum serviceQuarter = GetQuarter(datePlacedInService)

 
# INPUTS

# >>When was {1} placed in service?
TdateIn DatePlacedInService(Thing property) 
 
TstrIn ApplicableFirstYearConvention(Thing property)

TnumIn UnadjustedBasis(Thing property)

TnumIn PropertyClass(Thing property)

TnumIn RecoveryPeriod(Thing property)

# >>What method is being used to depreciate {1}? (Options: DB or SL)
TstrIn DepreciationMethod(Thing property)

    
# LISTED PROPERTY
  
TboolIn IsListedProperty(Thing person, Thing property)


# APPENDIX A: DEPRECIATION TABLES

# Returns the applicable depreciation percentage, as a time-varying percentage
Tnum DepreciationPercentage(Tbool isQIRP, Tstr system, Tstr method, Tnum recoveryPeriod, Tstr convention, Tstr assetClass, Tnum servicePeriodNumber) =
    set:
    if isQIRP -> DepreciationPercentageTablesA21ToA24(table, convention, servicePeriodNumber)
    else DepreciationPercentageTablesA1ToA18(table, recoveryPeriod)
    
    Tnum table = 
        ApplicableDepreciationTable(system, method, recoveryPeriod, convention, assetClass, servicePeriodNumber)    

# Charts 1 & 2, Appendix A, p. 75.
Tnum ApplicableDepreciationTable(Tstr system, Tstr method, Tnum recoveryPeriod, Tstr convention, Tstr assetClass, Tnum serviceQtr) =
    Stub()

# Returns a time-varying depreciation percentage, given the applicable
# table in Pub. 946 and the recovery period in years.
# See Pub. 946, Tables A-1 to A-18, pages 76-99.
Tnum DepreciationPercentageTablesA1ToA18(Tnum table, Tnum recoveryPeriod) =
    Stub()

# Returns a time-varying income inclusion percentage, given the applicable
# table in Pub. 946 and the first tax year during lease in which business
# use is 50% or less.
# See Tables A-19, A-20, p.100. 
Tnum IncomeInclusionRateTablesA19ToA20(Tnum table, Tnum yearNumber) =
    Stub()
    
# Returns a time-varying depreciation percentage, given the applicable
# table in Pub. 946, the convention used, and the period number the property
# was placed in service.
# Conventions: Half-year, Mid-quarter, Mid-month
# See Qualified Indian Reservation Property tables, A-21 to A-24, pages 101-102.
Tnum DepreciationPercentageTablesA21ToA24(Tnum table, Tstr convention, Tnum servicePeriodNumber) =
    Stub()

    
# APPENDIX B: ASSET CLASSES

# Data from Tables B-1 and B2, pages 103-113.
# Column names: Asset class, Class life, GDS recovery period, ADS recovery period
Tnum TableBValue(Tstr assetDescription, Tstr columnName) =
    Stub()
    
    
# UNIT TESTS

Test: DBRate1
- IRS.Pub946.DecliningBalanceRate(3).Out =?= 0.66667

Test: FirstYearPercentage1
- IRS.Pub946.FirstYearPercentage("Mid-month", 2011-08-04).Out =?= 0.375

Test: StraightLineRate1
- IRS.Pub946.StraightLineRate(39).Out =?= (decimal)0.02564

Test: AdjustedBasis_sanity
- Thing prop
- UnadjustedBasis(prop) = 1000
- DepreciationMethod(prop) = "DB"
- IRS.Pub946.AdjustedBasis(prop, 1).Out =?= 1000

Test: ComputedRate_Pub946_Page49_Ex1_Year1
- Thing prop
- UnadjustedBasis(prop) = 1000
- RecoveryPeriod(prop) = 5
- ApplicableFirstYearConvention(prop) = "Half-year"
- DepreciationMethod(prop) = "DB"
- IRS.Pub946.Deduction(prop, 1).Out =?= 200

Test: ComputedRate_Pub946_Page49_Ex1_Year2_AdjustedBasis
- Thing prop
- UnadjustedBasis(prop) = 1000
- RecoveryPeriod(prop) = 5
- ApplicableFirstYearConvention(prop) = "Half-year"
- DepreciationMethod(prop) = "DB"
- IRS.Pub946.AdjustedBasis(prop, 2).Out =?= 800

Test: ComputedRate_Pub946_Page49_Ex1_Year2
- Thing prop
- UnadjustedBasis(prop) = 1000
- RecoveryPeriod(prop) = 5
- ApplicableFirstYearConvention(prop) = "Half-year"
- DepreciationMethod(prop) = "DB"
- IRS.Pub946.Deduction(prop, 2).Out =?= 320

Test: ComputedRate_Pub946_Page49_Ex1_Year3
- Thing prop
- UnadjustedBasis(prop) = 1000
- RecoveryPeriod(prop) = 5
- ApplicableFirstYearConvention(prop) = "Half-year"
- DepreciationMethod(prop) = "DB"
- IRS.Pub946.Deduction(prop, 3).Out =?= 192

Test: ComputedRate_Pub946_Page49_Ex1_Year4
- Thing prop
- UnadjustedBasis(prop) = 1000
- RecoveryPeriod(prop) = 5
- ApplicableFirstYearConvention(prop) = "Half-year"
- DepreciationMethod(prop) = "DB"
- IRS.Pub946.Deduction(prop, 4).Out =?= 115

Test: ComputedRate_Pub946_Page49_Ex1_Year5
- Thing prop
- UnadjustedBasis(prop) = 1000
- RecoveryPeriod(prop) = 5
- ApplicableFirstYearConvention(prop) = "Half-year"
- DepreciationMethod(prop) = "DB"
- IRS.Pub946.Deduction(prop, 5).Out =?= 115

Test: ComputedRate_Pub946_Page49_Ex1_Year6
- Thing prop
- UnadjustedBasis(prop) = 1000
- RecoveryPeriod(prop) = 5
- ApplicableFirstYearConvention(prop) = "Half-year"
- DepreciationMethod(prop) = "DB"
- IRS.Pub946.Deduction(prop, 6).Out =?= 58

Test: ComputedRate_Pub946_Page50_Ex2_Year1_FYP
- IRS.Pub946.FirstYearPercentage("Mid-month", 2011-01-02).Out =?= (decimal)0.958

Test: ComputedRate_Pub946_Page50_Ex2_Year1
- Thing prop
- UnadjustedBasis(prop) = 100000
- RecoveryPeriod(prop) = 39
- DatePlacedInService(prop) = 2011-01-02
- ApplicableFirstYearConvention(prop) = "Mid-month"
- DepreciationMethod(prop) = "SL"
- IRS.Pub946.Deduction(prop, 1).Out =?= 2456 

Test: ComputedRate_Pub946_Page50_Ex2_Year2
- Thing prop
- UnadjustedBasis(prop) = 100000
- RecoveryPeriod(prop) = 39
- DatePlacedInService(prop) = 2011-01-02
- ApplicableFirstYearConvention(prop) = "Mid-month"
- DepreciationMethod(prop) = "SL"
- IRS.Pub946.Deduction(prop, 2).Out =?= 2564

Test: ComputedRate_Pub946_Page50_Ex2_Year3
- Thing prop
- UnadjustedBasis(prop) = 100000
- RecoveryPeriod(prop) = 39
- DatePlacedInService(prop) = 2011-01-02
- ApplicableFirstYearConvention(prop) = "Mid-month"
- DepreciationMethod(prop) = "SL"
- IRS.Pub946.Deduction(prop, 3).Out =?= 2564

Test: ComputedRate_Pub946_Page50_Ex3_Safe_Year1
- Thing prop
- UnadjustedBasis(prop) = 4000
- RecoveryPeriod(prop) = 7
- DatePlacedInService(prop) = 2011-01-02
- ApplicableFirstYearConvention(prop) = "Mid-quarter"
- DepreciationMethod(prop) = "DB"
- IRS.Pub946.Deduction(prop, 1).Out =?= 1000

Test: ComputedRate_Pub946_Page50_Ex3_Safe_Year2
- Thing prop
- UnadjustedBasis(prop) = 4000
- RecoveryPeriod(prop) = 7
- DatePlacedInService(prop) = 2011-01-02
- ApplicableFirstYearConvention(prop) = "Mid-quarter"
- DepreciationMethod(prop) = "DB"
- IRS.Pub946.Deduction(prop, 2).Out =?= 857

Test: ComputedRate_Pub946_Page50_Ex3_Furniture_Year1
- Thing prop
- UnadjustedBasis(prop) = 1000
- RecoveryPeriod(prop) = 7
- DatePlacedInService(prop) = 2011-09-02
- ApplicableFirstYearConvention(prop) = "Mid-quarter"
- DepreciationMethod(prop) = "DB"
- IRS.Pub946.Deduction(prop, 1).Out =?= 107

Test: ComputedRate_Pub946_Page50_Ex3_Furniture_Year2
- Thing prop
- UnadjustedBasis(prop) = 1000
- RecoveryPeriod(prop) = 7
- DatePlacedInService(prop) = 2011-09-02
- ApplicableFirstYearConvention(prop) = "Mid-quarter"
- DepreciationMethod(prop) = "DB"
- IRS.Pub946.Deduction(prop, 2).Out =?= 255

Test: ComputedRate_Pub946_Page50_Ex3_Computer_Year1
- Thing prop
- UnadjustedBasis(prop) = 5000
- RecoveryPeriod(prop) = 5
- DatePlacedInService(prop) = 2011-10-02
- ApplicableFirstYearConvention(prop) = "Mid-quarter"
- DepreciationMethod(prop) = "DB"
- IRS.Pub946.Deduction(prop, 1).Out =?= 250

Test: ComputedRate_Pub946_Page50_Ex3_Computer_Year2
- Thing prop
- UnadjustedBasis(prop) = 5000
- RecoveryPeriod(prop) = 5
- DatePlacedInService(prop) = 2011-10-02
- ApplicableFirstYearConvention(prop) = "Mid-quarter"
- DepreciationMethod(prop) = "DB"
- IRS.Pub946.Deduction(prop, 2).Out =?= 1900

Test: ComputedRate_Pub946_Page51_Ex4_Year1
- Thing prop
- UnadjustedBasis(prop) = 7500
- RecoveryPeriod(prop) = 7
- DatePlacedInService(prop) = 2011-07-02
- ApplicableFirstYearConvention(prop) = "Half-year"
- DepreciationMethod(prop) = "DB"
- IRS.Pub946.Deduction(prop, 1).Out =?= 1071  # $1 diff b/c example uses tables
    
    