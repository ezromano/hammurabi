# Citation:     26 U.S.C. 6013 (2010)
# Namespace:    USC.Tit26.Sec6013
# Summary:      Joint tax returns under Internal Revenue Code, Section 6013.
# Updated:      2012-05-29
# Author:       Michael Poulshock


# MARRIED FILING JOINTLY

# Two married taxpayers are filing a joint return.
Tbool AreMFJ(Thing p1, Thing p2) =
	Fam.AreMarried(p1,p2) &
	...
		Sec2.FilingStatus(p1) == "Married filing jointly" |~
		Sec2.FilingStatus(p2) == "Married filing jointly"

# Person is married filing jointly
Tbool IsMFJ(Thing p) =
	Sec2.FilingStatus(p) == "Married filing jointly" |~		# Person indicated MFJ
	Fam.SpousesOf(p).Exists(AreMFJ(p,_))					    # Spouse indicated MFJ
	
	##
	Thing spouse =
		Fam.SpouseOf(p)
	##
	
# JOINT RETURN TO CLAIM A REFUND

# Filing a joint return merely to claim a refund.
TboolInSym FileMFJOnlyToClaimRefund(Thing p1, Thing p2)

# Person is filing a joint return
TboolIn FilesMFJOnlyToClaimRefund(Thing p) =
	Fam.SpousesOf(p).Exists(FileMFJOnlyToClaimRefund(p,_))

		
# UNIT TESTS

Test: IsMFJ_1a 
- Thing a
- Thing b
- FamilyRelationship(a,b) = "Spouse"
- FedTaxFilingStatus(a) = "Married filing jointly"
- Sec6013.IsMFJ(a).Out =?= true

Test: IsMFJ_1b
- Thing a
- Thing b
- FamilyRelationship(a,b) = "Spouse"
- FedTaxFilingStatus(a) = "Married filing jointly"
- Sec6013.IsMFJ(b).Out =?= true

Test: IsMFJ_1c 
- Thing a
- Thing b
- FamilyRelationship(a,b) = "Spouse"
- FedTaxFilingStatus(a) = "not Married filing jointly"
- Sec6013.IsMFJ(a).Out =?= false

Test: IsMFJ_1d
- Thing a
- Thing b
- FamilyRelationship(a,b) = "Spouse"
- FedTaxFilingStatus(a) = "not Married filing jointly"
- Sec6013.IsMFJ(b).Out =?= false


Test: IsMFJ_2a
- Thing p
- FedTaxFilingStatus(p) = "not Married filing jointly"
- Sec6013.IsMFJ(p).Out =?= false

Test: IsMFJ_2b
- Thing p
- FedTaxFilingStatus(p) = "Married filing jointly"
- Sec6013.IsMFJ(p).Out =?= true

Test: IsMFJ_3
- Thing p
- FedTaxFilingStatus(p) = "Single"
- Sec6013.IsMFJ(p).Out =?= false

Test: AreMFJ_1 
- Thing a
- Thing b
- FamilyRelationship(a,b) = "Spouse"
- FedTaxFilingStatus(a) = "Married filing jointly"
- Sec6013.AreMFJ(a,b).Out =?= true

Test: AreMFJ_2
- Thing a
- Thing b
- FamilyRelationship(a,b) = "Siblings"
- FedTaxFilingStatus(a) = "Married filing jointly"
- Sec6013.AreMFJ(a,b).Out =?= false

Test: AreMFJ_3
- Thing a
- Thing b
- FamilyRelationship(a,b) = "Spouse"
- FedTaxFilingStatus(a) = "Head of household"
- Sec6013.AreMFJ(a,b).Out =?= false




