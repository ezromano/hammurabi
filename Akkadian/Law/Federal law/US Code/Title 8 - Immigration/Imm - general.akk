# Namespace:    USC.Tit8.Gen
# Summary:      Some general immigration related rules
# Source:       Nolo Press, "U.S. Immigration Made Easy" (15th ed.)
# Remarks:      Experimental; rules will later have to be checked and moved in line with U.S. Code.
# Updated:      2012-05-06
# Author:       Michael Poulshock


# GENERAL CONCEPTS

# Type of visa the person has/is applying for
TstrIn VisaType(Person p)



# CITIZENSHIP, DEFINED

# U.S. citizenship (inferred)
Tbool IsUSCitizenInferred(Person p) =
	BirthCountry(p) == "United States" |
	CitizenByBirthToCitizen(p) |
	DerivationOfCitizenship(p)

# Citizenship due to birth to citizen parents
Tbool CitizenByBirthToCitizen(Person p) =
	set:
	if dob < 1986-11-14 -> Stub()
	else currentRule
	
	Tbool currentRule =
		...
			...
				Fam.ParentsOf(p).ForAll(CitizenAsOf(_, dob)) &		 	# Both parents citizens at p's birth
				Fam.ParentsOf(p).Exists(ResidentOfUSPriorTo(_, dob))	# One parent lived in US before p's birth
			|
			...
			Fam.ParentsOf(p).Exists(MeetsOneParentPost1986Test(_, p))	# One parent citizen at p's birth...
		&
		if Fam.IsIllegitimate(p) then legitimationTest &
		residencyTest
		
	Tdate dob =
		DoB(p)		

	Tbool legitimationTest =
		Fam.Legitimated(dad, p).IsEverTrueBefore(bday18) &
		Fam.PaternityEstablishedFor(dad, p).IsEverTrueBefore(bday18) &
		Fam.CommittedToFinanciallySupport(dad, p).IsEverTrueBefore(bday18)
		
	Person dad =
		Fam.FatherAsOf(p, bday18)
		
	Tdate bday18 =
		DoB(p).AddYears(18)
	
	Tbool residencyTest =
		Stub()

# Citizenship due to naturalization of a parent
Tbool DerivationOfCitizenship(Person p) =
	set:
	if currentTestApplies -> currentRule
	else Stub()
	
	Tbool currentTestApplies =
		Fam.ParentsOf(p).Exists(BornInTheUSA(_)) |
		Fam.ParentsOf(p).Exists(NaturalizedAfter20010227(_))
	
	Tbool currentRule =
		Stub()			
		
# Naturalized after 2001-02-27
Tbool NaturalizedAfter20010227(Person p) =
	Imm.IsNaturalizedCitizen(p).DateFirstTrue >= 2001-02-27

# U.S. citizen as of a given date	
Tbool CitizenAsOf(Person p, Tdate d) =
	IsUSCitizen(p).AsOf(d)

# U.S. resident prior to a given date
Tbool ResidentOfUSPriorTo(Person p, Tdate date) =
	IsUSResident(p).IsEverTrue(Time.DawnOf, date)
	
# Post-1986 rule for children born to one U.S. citizen parent
Tbool MeetsOneParentPost1986Test(Person parent, Person child) =
	CitizenAsOf(parent, DoB(child)) &
	IsUSResident(parent).ElapsedYearsBefore(dob) >= 5 &
	residentAge14.ElapsedYearsBefore(dob) >= 2
	
	Tbool residentAge14 =
		IsUSResident(parent) &
		Age(parent) >= 14
	
	Tdate dob =
		DoB(child)
		
		