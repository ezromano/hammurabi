# Citation:     8 U.S.C. 1408 (2012)
# Namespace:    USC.Tit8.Sec1408
# Summary:      Nationals but not citizens of the U.S. at birth
# Updated:      2012-07-12
# Author:       Michael Poulshock


# U.S. national (but not citizen) by birth
Tbool IsUSNationalByBirth(Thing p) = 
	...
		Para1(p) |
		Para2(p) |
		Para3(p) |
		Para4(p)
	&
	TheTime.IsAtOrAfter(DoB(p))		# Status as national starts at birth
	
	
# Paragraph (1) - Born in outlying possession on or after U.S. annexation
Tbool Para1(Thing p) =
	Sec1101.IsUSOutlyingPossession(place) &
	...
		place == "American Samoa" &
		DoB(p) >= 1911-07-17         # http://en.wikipedia.org/wiki/American_Samoa
	&
	...
		place == "Swains Island" &
		DoB(p) >= 1925-03-04         # http://en.wikipedia.org/wiki/Swains_Island
		
	Tstr place = 
		BirthCountry(p)

		
# Paragraph (2) - Both parents nationals
Tbool Para2(Thing p) =
	! Sec1401.BornInUSorOP(p) &
	...
		Sec1401.ParentsMarried(p) |
		Sec1409.SubsecAApplies(p)
	&
	Sec1401.BothParentsNationals(p) &
	Sec1401.ParentResidedInUSorOPBeforeBirthOf(p)

	
# Paragraph (3) - Of unknown parentage, found in outlying possession
Tbool Para3(Thing p) =
	Fam.IsOfUnknownParentage(p) &
	Stub()


# Paragraph (4) - One parent national, one parent alien
Tbool Para4(Thing p) =
	! Sec1401.BornInUSorOP(p) &
	Sec1401.ParentsMarried(p) &
	Sec1401.OneNationalOneAlienParent(p) 
	# &
	# Sec1401.BirthParents(p).Exists(Para4ParentTest(_, DoB(p)))

# National parent meets complex time-based test for physical presence
Tbool Para4ParentTest(Thing parent, Tdate date) =
	Imm.IsUSNational(parent) &
	present7YearsIn10.IsEverTrueBefore(date) &
	fiveYearTestMet.IsEverTrueBefore(date) &
	Stub()   # Para (4) not handled completely b/c it involves analysis of a sliding
	         # window of time during which several facts are considered.  Also, rare.
	
	# (4) - present for a total of seven years in any continuous period of ten years
	Tbool present7YearsIn10 =
		present.ElapsedYearsInSlidingWindow(10, "Year") >= 7
	
	# (4)(B)
	Tbool fiveYearTestMet =
		present.RunningElapsedYears >= 5 &
		Age(parent) >= 14
	
	# "Physically present"
	Tbool present =
		Sec1401.PresentUnderSubsecG(parent)  

	##
	# (4)(A) - A one year absence starts the clock over...
	Tbool notOutsideOneYear =
		outside.RunningElapsedYears <= 1
		
	Tbool outside = 
		! Sec1401.PresentUnderSubsecG(parent)
	##	
	

	