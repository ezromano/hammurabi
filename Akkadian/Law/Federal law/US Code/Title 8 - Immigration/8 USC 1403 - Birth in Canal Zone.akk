# Citation:     8 U.S.C. 1403 (2012)
# Namespace:    USC.Tit8.Sec1403
# Summary:      Persons born in Canal Zone or Panama on or after 1904-02-26
# Updated:      2012-09-14
# Author:       Michael Poulshock


# Citizenship of people born in Canal Zone or Panama
Tbool IsUSCitizenPerCanalZone(Thing p) = 
	SubsecA(p) |
	SubsecB(p)
	
# Birth in Canal Zone
Tbool SubsecA(Thing p) =
	BirthCountry(p) == "United States" &
	BirthState(p) == "Panama Canal Zone" &
	DoB(p) >= 1904-02-26 &
	Sec1401.BirthParents(p).ForAll(IsUSCitizen(_)) &
	TheTime.IsAtOrAfter(DoB(p))    # Makes conclusion true from DoB onward
	
# Birth in Panama
Tbool SubsecB(Thing p) =
	BirthCountry(p) == "Panama" &
	DoB(p) >= 1904-02-26 &
	ParentTest(p).AsOf(DoB(p)) &
	TheTime.IsAtOrAfter(DoB(p))    # Makes conclusion true from DoB onward
	
Tbool ParentTest(Thing p) =
	Sec1401.BirthParents(p).Exists(IsUSCitizenOrQualifyingEmployee(_))
	
# U.S. citizen and U.S. gov't/canal employee
Tbool IsUSCitizenOrQualifyingEmployee(Thing p) =
	IsUSCitizen(p) &
	...
		Econ.IsUSGovtEmployee(p) |
		Econ.IsPanamaRailroadEmployee(p)
	

# UNIT TESTS

Test: 619099897
- Thing leon
- BirthState(leon) = "Panama"
- Things sam
- FatherAtTimeOfBirth(leon) = [[sam]]
- Things minerva
- MotherAtTimeOfBirth(leon) = [[minerva]]
- IsUSCitizen(sam) = true
- IsUSGovtEmployee(sam) = true
- IsUSCitizen(minerva) = false
- BirthCountry(leon) = "Panama"
- DoB(leon) = 1978-03-22
- USC.Tit8.Sec1403.IsUSCitizenPerCanalZone(leon).TestOutput =?= "Time.DawnOf False 3/22/1978 12:00:00 AM True "

Test: 708272036
- Thing ted
- BirthCountry(ted) = "United States"
- BirthState(ted) = "Panama Canal Zone"
- DoB(ted) = 1988-03-09
- Things jed
- FatherAtTimeOfBirth(ted) = [[jed]]
- Things edna
- MotherAtTimeOfBirth(ted) = [[edna]]
- USImmigrationStatus(jed) = "U.S. citizen"
- USImmigrationStatus(edna) = "U.S. citizen"
- USC.Tit8.Sec1403.IsUSCitizenPerCanalZone(ted).TestOutput =?= "Time.DawnOf False 3/9/1988 12:00:00 AM True "


