# Namespace:    Pa.Code.Tit55.Ch601
# Summary:      Low Income Home Energy Assistance Program (LIHEAP)
# Updated:      2013-03-01
# Author:       Michael Poulshock


# 601.31 - GENERAL ELIGIBILITY REQUIREMENTS

# >>Does {1} meet the general eligibility requirements for LIHEAP in Pennsylvania?
Tbool IsEligible(Thing household) =
    MeetsIncomeTest(household) &
    ResponsibleForHeatingCosts(household) &
    PaResidents(household) &
    LawfullyPresent(household)
    
# Income test
# TODO: Countable household members only?
Tbool MeetsIncomeTest(Thing hh) =
    LIHEAPIncome(hh) <= USC.Tit42.Sec9902.FPGPercentage(Mems(hh).Count, "Pennsylvania", threshold)

    Tnum threshold =
        set:
        from 2012-12-31 -> Stub()
        from 2011-01-01 -> 1.50     # TODO: Check these dates
        else Stub()

# >>Is {1} responsible for home heating costs?
Tbool ResponsibleForHeatingCosts(Thing hh)
    
# At least one household member is a PA resident (assumption)
Tbool PaResidents(Thing hh) =
    Mems(hh).Exists(StateOfResidence(_) == "Pennsylvania")
    
# At least one household member is lawfully present in the U.S. (assumption)
Tbool LawfullyPresent(Thing hh) =
    Mems(hh).Exists(IsLawfullyPresent(_))
   
# Lawful presence
Tbool IsLawfullyPresent(Thing p) =
    Imm.IsUSCitizen(p) |
    IsQLAN(p)
    
# Qualified lawfully admitted noncitizen
Tbool IsQLAN(Thing p) =
    USC.Tit8.Sec1641.IsQualifiedAlien(p)
    

# HOUSEHOLD SIZE

# 601.41(1) - Household size
Tnum HouseholdSize(Thing hh) =
    CountableMembers(hh).Count

# Countable household members
Tset CountableMembers(Thing hh) =
    Mems(hh).Filter(IsCountable(_))

# Household member should be counted
Tbool IsCountable(Thing p) =
    ! PreviouslyReceivedLIHEAPInAnotherHousehold(p) &
    Econ.LivingArrangement(p) <> "Temporary living arrangement" &
    Econ.LivingArrangement(p) <> "Reside in institution" &
    ! IsQLAN(p) &
    ! IsInPrison(p) &
    ! IsFleeingFelon(p)     # TODO: Left out people fleeing NJ high misdemeanors
     
# Household members
Tset Mems(Thing household) =
    Econ.HouseholdMembers(household)
    
    
# COUNTABLE INCOME
# Note: All values are annual

# 601.41(2) - LIHEAP income
Tnum LIHEAPIncome(Thing hh) =
    set:
    if AnnualEmploymentIncome(hh) > 0 -> TotalCountableIncome(hh) - (Econ.AnnualWages(hh) * 0.20)
    else TotalCountableIncome(hh)
    
Tnum AnnualEmploymentIncome(Thing hh) =
    MembersWithCountableIncome(hh).Sum(Econ.AnnualWages(_))

# Total countable income from all countable household members
Tnum TotalCountableIncome(Thing hh) =
    MembersWithCountableIncome(hh).Sum(CountableIncome(_))
    
Tset MembersWithCountableIncome(Thing hh) =
    Mems(hh).Filter(IncomeIsCounted(_))

# 601.81
Tbool IncomeIsCounted(Thing p) = 
    true        # TODO: Refine...

# Total countable income
Tnum CountableIncome(Thing p) =
    GrossIncome(p) - ExcludedIncome(p)

# 601.82
Tnum GrossIncome(Thing p) =
    EarnedIncome(p) + UnearnedIncome(p)

Tnum EarnedIncome(Thing p) =
    Econ.AnnualWages(p) +
    Ch183.Sec65.MonthlyProfit(p) * 12      # Encompasses both 601.82(2) and (3)

Tnum UnearnedIncome(Thing p) =
    Econ.AnnualUnearnedIncome(p) 
            
# 601.84
# >>How much of {1}'s income should be excluded under 55 Pa. Code 601.84?
Tnum ExcludedIncome(Thing p)
    
             
# GENERAL LIHEAP BENEFIT AMOUNT

# Depends on: PACountyOfResidence, LIHEAPIncome, Econ.HeatingFuelType, and HouseholdSize.
# If the rules can't be found elsewhere, they have to be reverse engineered from this webpage:
# http://www.dpw.state.pa.us/foradults/heatingassistanceliheap/liheapbenefitamounttable/index.htm


# INPUTS 

# >>Did {1} previously receive a LIHEAP benefit in another household during the program year?
Tbool PreviouslyReceivedLIHEAPInAnotherHousehold(Thing person)

